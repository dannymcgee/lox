program    -> statement* EOF ;

statement  -> exprStmt
            | printStmt ;

exprStmt   -> expression ";" ;
printStmt  -> "print" expression ";" ;

expression -> equality
equality   -> comparison (( '!=' | '==' ) comparison )* ;
comparison -> term (( '>' | '>=' | '<' | '<=' ) term )* ;
term       -> factor (( '-' | '+' ) factor )* ;
factor     -> unary (( '/' | '*' ) unary )* ;
unary      -> ( '!' | '-' ) unary
            | primary ;
primary    -> NUMBER | STRING | 'true' | 'false' | 'nil'
            | '(' expression ')' ;

# precedence:
# NAME           OPERATORS    ASSOCIATES
# Equality       == !=        Left
# Comparison     > >= < <=    Left
# Term           - +          Left
# Factor         / *          Left
# Unary          ! -          Right
